buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("com.github.ben-manes:gradle-versions-plugin:${gradleVersionsVersion}")
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

allprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    group 'com.github.c4milojimenez'

    dependencies {
        implementation 'org.springframework:spring-context:5.3.24'

        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'

        testCompileOnly 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

        testImplementation 'org.mockito:mockito-core:3.12.4'
        testImplementation 'junit:junit:4.13.2'
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    ext {
        pomConfig = {
            licenses {
                license {
                    name "MIT License"
                    url "https://opensource.org/licenses/mit-license.php"
                    distribution "repo"
                }
            }

            scm {
                url "git@github.com:c4milojimenez/cognito.git"
            }
        }
        groupId = 'com.github.c4milojimenez'
    }

    group groupId

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    java {
        withJavadocJar()
        withSourcesJar()
    }

    repositories {
        mavenCentral()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                groupId = groupId
                artifactId = artifactId
                version = project.property('version')

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', artifactDescription)
                    root.appendNode('name', artifactId)
                    root.appendNode('url', 'https://github.com/c4milojimenez/cognito')
                    root.children().last() + pomConfig
                }
            }
        }
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    if (project.hasProperty('signing.keyId')) {
        signing {
            sign publishing.publications.mavenJava
        }
    }
}

tasks.named('wrapper') {
    gradleVersion = '7.6'
}

apply plugin: 'com.github.ben-manes.versions'
